envvars:
    "SPECFEM2D_BINDIR"

stations = [f"S{i:04d}" for i in range(1, 21)]
seismograms = {"BXX": ["semd", "semv", "sema"], "BXZ": ["semd", "semv", "sema"], "PRE": ["semp"]}

traces_specfem2d = [
    f"specfem2d_workdir/OUTPUT_FILES/AA.{station}.{component}.{seismogram}"
    for station in stations
    for component in seismograms.keys()
    for seismogram in seismograms[component]
]

traces = [
    f"traces/AA.{station}.S2.{component}.{seismogram}"
    for station in stations
    for component in seismograms.keys()
    for seismogram in seismograms[component]
]

rule all:
    input:
        traces


rule specfem2d_link_executibles:
    # prompt once for the binary directory and build both paths
    output:
        specfem_exec="specfem2d_workdir/bin/xspecfem2D",
        meshfem_exec="specfem2d_workdir/bin/xmeshfem2D",
    localrule: True
    shell:
        """
            ln -sf $SPECFEM2D_BINDIR/xspecfem2D {output.specfem_exec}
            ln -sf $SPECFEM2D_BINDIR/xmeshfem2D {output.meshfem_exec}
        """


rule specfem2d_copy_files:
    # prompt once for the binary directory and build both paths
    output:
        par_file="specfem2d_workdir/DATA/Par_file",
        source="specfem2d_workdir/DATA/SOURCE",
        topography="specfem2d_workdir/DATA/topography.dat",
    localrule: True
    shell:
        """
            cp provenance/Par_file {output.par_file}
            cp provenance/SOURCE {output.source}
            cp provenance/topography.dat {output.topography}
        """


rule specfem2d_run_mesher:
    input:
        meshfem_exec="specfem2d_workdir/bin/xmeshfem2D",
        par_file="specfem2d_workdir/DATA/Par_file",
        topography="specfem2d_workdir/DATA/topography.dat",
    output:
        mesh="specfem2d_workdir/OUTPUT_FILES/Database00000.bin",
        stations="specfem2d_workdir/DATA/STATIONS",
    localrule: True
    shell:
        """
            cd specfem2d_workdir/
            ./bin/xmeshfem2D
        """


rule specfem2d_run_solver:
    input:
        specfem_exec="specfem2d_workdir/bin/xspecfem2D",
        par_file="specfem2d_workdir/DATA/Par_file",
        source="specfem2d_workdir/DATA/SOURCE",
        topography="specfem2d_workdir/DATA/topography.dat",
        mesh="specfem2d_workdir/OUTPUT_FILES/Database00000.bin",
        stations="specfem2d_workdir/DATA/STATIONS",
    output:
        traces=traces_specfem2d,
    localrule: True
    shell:
        """
            cd specfem2d_workdir/
            ./bin/xspecfem2D
        """

rule specfem2d_collect_traces:
    input:
        traces=traces_specfem2d,
    output:
        traces=traces,
    localrule: True
    run:
        import os
        os.makedirs("traces", exist_ok=True)
        for i in range(len(input.traces)):
            infile = input.traces[i]
            outfile = output.traces[i]
            shell(f"mv {infile} {outfile}")

rule specfem2d_clean:
    localrule: True
    shell:
        """
            rm -rf specfem2d_workdir
        """
