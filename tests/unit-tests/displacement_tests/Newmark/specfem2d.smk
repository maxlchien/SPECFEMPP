envvars:
    "SPECFEM2D_BINDIR",
    "SPECFEMPP_BINDIR",

pathvars:
    cwd=os.getcwd()


rule specfem2d_setup:
    input:
        par_file="<cwd>/provenance/Par_file",
        source="<cwd>/provenance/SOURCE",
        topography="<cwd>/provenance/topography.dat",
    output:
        par_file="<cwd>/specfem2d_workdir/DATA/Par_file",
        source="<cwd>/specfem2d_workdir/DATA/SOURCE",
        topography="<cwd>/specfem2d_workdir/DATA/topography.dat",
        cwd=directory("<cwd>/specfem2d_workdir"),
    localrule: True,
    shell:
        """
            cp {input.par_file} {output.par_file}
            cp {input.source} {output.source}
            cp {input.topography} {output.topography}
        """


rule specfem2d_mesher:
    input:
        setup=rules.specfem2d_setup.output,
        cwd=rules.specfem2d_setup.output.cwd,
    output:
        database="<cwd>/specfem2d_workdir/OUTPUT_FILES/Database00000.bin",
        stations="<cwd>/specfem2d_workdir/DATA/STATIONS",
        mesher="<cwd>/specfem2d_workdir/OUTPUT_FILES/output_mesher.txt",
    shell:
        """
            cd {input.cwd}
            $SPECFEM2D_BINDIR/xmeshfem2D > OUTPUT_FILES/output_mesher.txt
        """


rule specfempp_mesher:
    input:
        setup=rules.specfem2d_setup.output,
        cwd=rules.specfem2d_setup.output.cwd,
    output:
        database="<cwd>/specfem2d_workdir/OUTPUT_FILES/database.bin",
    shell:
        """
            cd {input.cwd}/DATA
            $SPECFEMPP_BINDIR/xmeshfem2D -p Par_file
        """

rule specfempp_move_database:
    input:
        database=rules.specfempp_mesher.output.database,
    output:
        database="<cwd>/database.bin",
    shell:
        """
            mv {input.database} {output.database}
        """


rule specfem2d_solver:
    input:
        setup=rules.specfem2d_setup.output,
        mesher=rules.specfem2d_mesher.output,
        cwd=rules.specfem2d_setup.output.cwd,
    output:
        solver="<cwd>/specfem2d_workdir/OUTPUT_FILES/output_solver.txt"
    shell:
        """
            cd {input.cwd}
            $SPECFEM2D_BINDIR/xspecfem2D > OUTPUT_FILES/output_solver.txt
        """


rule specfem2d_move_traces:
    input:
        solver=rules.specfem2d_solver.output.solver,
        cwd=rules.specfem2d_setup.output.cwd,
    output:
        trace_list="<cwd>/traces/trace_list.txt",
    localrule: True,
    run:
        import os
        trace_dir = os.path.join(input.cwd, "../traces")
        solver_outdir = os.path.join(input.cwd, "OUTPUT_FILES")
        os.makedirs(trace_dir, exist_ok=True)

        trace_list = []

        for trace_file in os.listdir(solver_outdir):
            trace_file_split = trace_file.split(".")
            if len(trace_file_split) == 4 and trace_file_split[-1].startswith("sem"):
                trace_file_out = ".".join([trace_file_split[0], trace_file_split[1], "S2", trace_file_split[2], trace_file_split[3]])
                trace_list.append(trace_file_out + "\n")
                infile = os.path.join(solver_outdir, trace_file)
                outfile = os.path.join(trace_dir, trace_file_out)
                shell(f"mv {infile} {outfile}")

        with open(output.trace_list, "w") as f:
            f.writelines(trace_list)

        os.remove(os.path.join(solver_outdir, "output_solver.txt"))


rule clean:
    localrule: True
    shell:
        """
            rm -rf specfem2d_workdir
            rm -rf traces
            rm -f database.bin
        """
